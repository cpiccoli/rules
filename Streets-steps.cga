/**
 * File:    Streets-steps.cga
 * Created: 13 Jan 2014 16:21:10 GMT
 * Author:  piccolicbm
 */

/*Important notes:
This rule distributed as supplementary material to C. Piccoli 2018. Visualizing cityscapes of Classical antiquity. From early modern reconstruction drawings to digital 3D model. Oxford: Archaeopress.
It is optimized for the generation of the street network in Koroneia's 3D scene. Above a set slope threshold, the street turns automatically into steps of different depths.
*/

version "2015.2"


# ATTRIBUTES
@Group("COLORS")
attr green = "#00FF00"
attr yellow = "#FFFF00"
attr orange = "#FF8000"
attr red   = "#FF0000"
attr blue = " #4218f8"
attr lightGreen = "#CCFFCC"
attr lightPink = "#FFCCCC"

attr HighLOD 			= true	


#Textures
const stepsText = "assets/stoneblocks/StoneBlock.5.c.jpg"
const dirt_tex1 = fileRandom ("*/dirtmaps/dirtmap.*.jpg") 
const streetText = "assets/streets/Strada_terra_battuta.jpg"
const streetBump = "assets/streets/Strada_terra_battuta_BUMP.jpg"
attr StreetBumpValue = 0.5
const GREYWALL_C = "#FFFFFF"
const streetGrey = "assets/grey.png"

/*
Cf. streets's width in 4th century BC sites:
BOEOTIA:
Plataia= street connecting West and East tower (main street): 6.5m; acropolis area: 4.20-6.70m; South of acropolis: 4.50m-5.50m; East of acropolis: 4.90m-5.80 (Konency 2013, 123-125)
Tanagra= running NE-SW: ca. 3m (10ft); running SE-NW: ca. 5m (15ft) (Slapsak 2012, 56)
THESSALY:
Halos='Main avenue': 9.11m; Avenues A,B,C,D = 6.5-7.5m; others: 5.5m Reinders 1988, 208)
Goritsa= width varies along the street: major streets: average 4.95m; others: 3.25m (see Bakhuizen 1992, 171-245)
CHALKIDIKI:
Olynthos= Hoepfner and Schwandner 1994, 77: 5.05m; From Cahill 2002, ill. p. 28: ca. 5m and ca. 7.35m
PELOPONNESE:
Argolis/Halieis= Avenue A (underwater); Avenue B: 3.55-4.20m; Avenue C: 4.50-5.05m; crossing roads: 2.60-2.80m (Boyd-Rudolph 1978, 339-345)
Messenia/Messene= various width: from 2m to 12.6m (Müth 2007, 245-256 and table p. 274)
Arkadia/Kyparissia= six parallel streets: 4.60m; plateiai not found (traces seem to indicate a width of 8m) (Karapanagiotou 2005, 333)
EPIRUS:
Kassope= main street: 6m; 19 narrower streets: 4.50m (Schwandner in Isanger 2001, 109)
ASIA MINOR:
Ionia/Priene= 'Quellenthorstrasse': 5.60-6.18m; 'Athenastrasse': 3.64-4.35m; 'Westthorstrasse': 7-7.35; other streets: 3.10-5.60m (Wiegand and Schrader 1904, 48)
Ionia/Ephesos (3rd BC)= processional way: 8m; plataiai (E-W): 5m; stenopoi (N-W): 3m (Groh 2012, 69)
*/

@StartRule
Street --> alignScopeToAxes () 
			Lot2
Lot2-->	case convert(y, scope, world, pos, 0,1,0) - convert(y, scope, world, pos, 0,0,0) < 0 : reverseNormals
         print (slopeValueDegreesStreet)
         LotNormalUp
     	else: print (slopeValueDegreesStreet)
         LotNormalUp

slopeValueDegreesStreet = atan (scope.sy / ( scope.sx * float(scope.sx > scope.sz) + scope.sz * float(scope.sx <= scope.sz) ))

LotNormalUp --> case slopeValueDegreesStreet > 11 && slopeValueDegreesStreet < 14: Steps(0.30)
				case slopeValueDegreesStreet > 14 && slopeValueDegreesStreet < 18: Steps(0.25)
				case slopeValueDegreesStreet > 18 && slopeValueDegreesStreet < 27: Steps(0.20)
				case slopeValueDegreesStreet > 27 : NIL
				else: setPivot (xyz,1)Street1
				print (slopeValueDegreesStreet)
				/*
				From "Foothpats and Tracks. A Field Manual for their Construction and Improvement", 2002, p. 3-26: 
				Desiderable maximum gradient:
				For animal drawn carts: 8% (4.6°); 
				For pedestrians and packed animals: 12% (6.8°);
				as slope approaches 25% (14°) walking becomes difficult and steps are needed:
				Shallow steps required: 25% 
				Moderate steps required: 33% (18.3°)
				Steep steps required: 50% (26.6°)
				*/



Steps(DeepStep)--> 
	split (y) 
	{DeepStep : steps(scope.sy)
	}*
	
steps(yDim) --> 
	alignScopeToAxes(y)
	extrude(world.y, 1000)
	set(trim.vertical, false)
	split(y) {yDim+0.01 : BaseVolume | ~1 : NIL}


BaseVolume--> comp (f) 
						{all: Stepstext}
						 
Stepstext --> case HighLOD == true :
		setupProjection (0, scope.yx, 2.5, 5, rand (10), rand (10)) #'0.5, '1
					texture (stepsText)
					projectUV (0)
									
					setupProjection (2, scope.xy, '1, '1)
					set(material.dirtmap, dirt_tex1)
					projectUV (2)
		
		else : 		setupProjection (0, scope.yx, '1, '1)
					texture (streetGrey )
					projectUV (0)
					ColoredVolume.			

Street1 --> case HighLOD == true : 
			alignScopeToGeometry (yUp, 1)
			StreetTexture
			
			else : 	alignScopeToGeometry (yUp,1)
					setupProjection (0, scope.xz, '1, '1) 
					texture (streetGrey )
					projectUV (0)
					ColoredVolume.

StreetTexture--> case scope.sz>scope.sx :
			setupProjection (0, scope.xz, 5, 15) //ratio image: 1:3
					texture (streetText)
					projectUV (0)
									
					setupProjection (2, scope.xz, 5, 15)
					set(material.bumpmap, streetBump)
					set (material.bumpValue, StreetBumpValue )
					projectUV (2)
			
			else: setupProjection (0, scope.zx, 5, 15) //ratio image: 1:3
					texture (streetText)
					projectUV (0)
									
					setupProjection (2, scope.zx, 5, 15)
					set(material.bumpmap, streetBump)
					set (material.bumpValue, StreetBumpValue )
					projectUV (2)

Sidewalk --> Street //if sidewalks exist, they behave like streets - texture must be adjusted
SidewalkComp--> Street1
Crossing --> Street1
Junction--> Street1
JunctionEntry-->Street1






